//#version 430
layout(local_size_x = 16, local_size_y = 16, local_size_z = 1) in;

layout(binding = 0, rgba32f) readonly uniform highp image2D bulk;
layout(binding = 1, rgba32f) readonly uniform highp image2D surface;
layout(binding = 2, rgba32f) readonly uniform highp image2D stateIn;
layout(binding = 3, rgba32f) writeonly uniform highp image2D stateOut;

uniform float dx;
uniform float dt;
uniform float dryEps;

const float eps = 1e-6;

void main() {
    ivec2 pos = ivec2(gl_GlobalInvocationID.xy);
    ivec2 gridSize = imageSize(stateOut);

    if (pos.x <= 0 || pos.y <= 0 || pos.x >= gridSize.x - 1 || pos.y >= gridSize.y - 1) {
        imageStore(stateOut, pos, vec4(0.0, 0.0, 0.0, 1.0));
        return;
    }

    float h_prev = imageLoad(stateIn, pos).x;

    vec4 bulkc = imageLoad(bulk, pos);
    vec4 bulkr = imageLoad(bulk, min(pos + ivec2(1, 0), gridSize - ivec2(1)));
    vec4 bulkt = imageLoad(bulk, min(pos + ivec2(0, 1), gridSize - ivec2(1)));

    vec4 surfacec = imageLoad(surface, pos);
    vec4 surfacer = imageLoad(surface, min(pos + ivec2(1, 0), gridSize - ivec2(1)));
    vec4 surfacet = imageLoad(surface, min(pos + ivec2(0, 1), gridSize - ivec2(1)));

    float qx_c = bulkc.y + surfacec.y;
    float qx_r = bulkr.y + surfacer.y;
    float qy_c = bulkc.z + surfacec.z;
    float qy_t = bulkt.z + surfacet.z;

    float h_tilde_r = (surfacec.x + surfacer.x)*0.5;
    float h_bar_r = (bulkc.x + bulkr.x) * 0.5;
    float u_bar_r = (h_bar_r > eps) ? ( (bulkc.y + bulkr.y) * 0.5 ) / h_bar_r : 0.0;
    float q_breve_x_r = h_tilde_r * u_bar_r;

    float q_breve_x_c = surfacec.x * ((bulkc.x > eps) ? bulkc.y / bulkc.x : 0.0);

    float h_tilde_t =  (surfacec.x + surfacet.x) * 0.5 ;
    float h_bar_t =  (bulkc.x + bulkt.x) * 0.5;
    float v_bar_t = (h_bar_t > eps) ? ( (bulkc.z + bulkt.z) * 0.5) / h_bar_t : 0.0;
    float q_breve_y_t = h_tilde_t * v_bar_t;


    float q_breve_y_c = surfacec.x * ((bulkc.x > eps) ? bulkc.z / bulkc.x : 0.0);

    float div_q = (qx_r - qx_c) / dx + (qy_t - qy_c) / dx;
    float div_q_breve = (q_breve_x_r - q_breve_x_c) / dx + (q_breve_y_t - q_breve_y_c) / dx;

    float new_h = h_prev - dt * (div_q + div_q_breve);
    new_h = max(new_h, 0.0);

    vec2 total_q = vec2(qx_c, qy_c);

    imageStore(stateOut, pos, vec4(new_h, total_q.x, total_q.y, 1.0));
}