//#version 430
layout(local_size_x = 16, local_size_y = 16, local_size_z = 1) in;

layout(binding = 0, rgba32f) readonly uniform highp image2D bulk;
layout(binding = 1, rgba32f) readonly uniform highp image2D surface;

layout(binding = 2, rgba32f) writeonly uniform highp image2D stateOut;

uniform float dx;
uniform float dt;
uniform float dryEps;

void main() {
    ivec2 pos = ivec2(gl_GlobalInvocationID.xy);
    ivec2 gridSize = imageSize(stateOut);

    if (pos.x <= 0 || pos.y <= 0 || pos.x >= gridSize.x - 1 || pos.y >= gridSize.y - 1) {
        imageStore(stateOut, pos, vec4(0.0, 0.0, 0.0, 1.0));
        return;
    }

    vec4 bulkc = imageLoad(bulk, pos);
    vec4 bulkr = imageLoad(bulk, pos + ivec2(1, 0));
    vec4 bulkt = imageLoad(bulk, pos + ivec2(0, 1));

    vec4 surfacec = imageLoad(surface, pos);
    vec4 surfacer = imageLoad(surface, pos + ivec2(1, 0));
    vec4 surfacet = imageLoad(surface, pos + ivec2(0, 1));

    float total_h = bulkc.x + surfacec.x;
    

    float qx_c = bulkc.y + surfacec.y;
    float qx_r = bulkr.y + surfacer.y;
    float qy_c = bulkc.z + surfacec.z;
    float qy_t = bulkt.z + surfacet.z;

    float div_q = (qx_r - qx_c) / dx + (qy_t - qy_c) / dx;

    float new_h = total_h - div_q * dt;
    new_h = max(new_h, 0.0);

    vec2 total_q = vec2(qx_c, qy_c);

    if (new_h < dryEps) {
        new_h = 0.0;
        total_q = vec2(0.0);
    }

    imageStore(stateOut, pos, vec4(new_h, total_q.x, total_q.y, 1.0));
}