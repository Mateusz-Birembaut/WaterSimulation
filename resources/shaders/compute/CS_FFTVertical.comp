/*
MIT License

Copyright (c) 2021 Achal Pandey

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the "Software"), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all
copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
SOFTWARE.
*/
// Original shader source : https://github.com/achalpandeyy/OceanFFT
// Inspired from : http://wwwa.pikara.ne.jp/okojisan/otfft-en/stockham1.html

#define WORK_GROUP_DIM 256

#define PI 3.14159265358979323846

layout (local_size_x = WORK_GROUP_DIM) in;

layout (binding = 0, rg32f) uniform readonly image2D u_input;
layout (binding = 1, rg32f) uniform writeonly image2D u_output;

uniform int u_total_count;
uniform int u_subseq_count;
uniform int u_direction;      // 1 for FFT, -1 for IFFT
uniform float u_normalization; // 1.0 for FFT, 1/N for IFFT (applied per 1D pass)

vec2 MultiplyComplex(vec2 a, vec2 b)
{
	return vec2(a[0] * b[0] - a[1] * b[1], a[1] * b[0] + a[0] * b[1]);
}

vec4 ButterflyOperation(vec2 a, vec2 b, vec2 twiddle)
{
	vec2 twiddle_b = MultiplyComplex(twiddle, b);
	vec4 result = vec4(a + twiddle_b, a - twiddle_b);
	return result;
}

void main()
{
	ivec2 pixel_coord = ivec2(gl_WorkGroupID.x, gl_LocalInvocationID.x);

	int thread_count = int(u_total_count * 0.5f);
	int thread_idx = pixel_coord.y;

	int in_idx = thread_idx & (u_subseq_count - 1);		
	int out_idx = ((thread_idx - in_idx) << 1) + in_idx;

	float angle = float(u_direction) * -PI * (float(in_idx) / float(u_subseq_count));
	vec2 twiddle = vec2(cos(angle), sin(angle));

	vec4 a = imageLoad(u_input, ivec2(pixel_coord.x, pixel_coord.y));
	vec4 b = imageLoad(u_input, ivec2(pixel_coord.x, pixel_coord.y + thread_count));

	// Transforming two complex sequences independently and simultaneously

	vec4 result0 = ButterflyOperation(a.xy, b.xy, twiddle) * u_normalization;
	vec4 result1 = ButterflyOperation(a.zw, b.zw, twiddle) * u_normalization;

	imageStore(u_output, ivec2(pixel_coord.x, out_idx), vec4(result0.xy, result1.xy));
	imageStore(u_output, ivec2(pixel_coord.x, out_idx + u_subseq_count), vec4(result0.zw, result1.zw));
}